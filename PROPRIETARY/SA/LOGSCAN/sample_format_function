#
# This is a simple line format function for the log scanner
#

set MSGTYPES { "NULL" "PONG" "PACK" "MAST" "REPO" "RACK" "FRWD" "FACK" }

proc fmt { ts bytes } {

	global MSGTYPES lastts maxsil
##
## Tarp header:
##
##	msg_type	1 byte
##	seq_no		1 byte
##	snd		1 word
##	rcv		1 word
##	hoc		7 bits
##	prox		1 bit
##	hco		7 bits
##	weak		1 bit
##
	set output ""

	set length [llength $bytes]

	if { $length < 10 } {
		return "packet too short"
	}

	set nid [expr { [lindex $bytes 0] | ([lindex $bytes 1] << 8) }]
	set mty [lindex $bytes 2]
	if { $mty < [llength $MSGTYPES] } {
		set mty [lindex $MSGTYPES $mty]
	} else {
		set mty [format "  %1x" $mty]
	}
	set seq [lindex $bytes 3]
	set snd [expr { [lindex $bytes 4] | ([lindex $bytes 5] << 8) }]
	set rcv [expr { [lindex $bytes 6] | ([lindex $bytes 7] << 8) }]
	set hoc [expr { [lindex $bytes 8] & 0x7f }]
	set pro [expr { [lindex $bytes 8] & 0x80 }]
	set hco [expr { [lindex $bytes 9] & 0x7f }]
	set wea [expr { [lindex $bytes 9] & 0x80 }]
	set rss [expr { ([lindex $bytes end-1] + 128) & 0xff }]
	set lqa [expr { [lindex $bytes end] & 0x7f }]
	set chs [expr { [lindex $bytes end] & 0x80 }]

	set bytes [lrange $bytes 10 end-2]

	set output "NID=[format %1x $nid], TYP=$mty, RSS=[format %1d $rss],\
		LQA=[format %1d $lqa], CHS="

	if $chs {
		append output "OK"
	} else {
		append output "BAD"
	}

	append output ", SND=[format %1d $snd], RCV=[format %1d $rcv]\n"

	append output "         HOC=[format %1d $hoc], HCO=[format %1d $hco],\
		PRO="

	if $pro {
		append output "YES"
	} else {
		append output "NO"
	}

	append output ", WEA="

	if $wea {
		append output "YES"
	} else {
		append output "NO"
	}

	if [info exists lastts] {
		set sil [format %1d [expr { $ts - $lastts }]]
		if { $sil > $maxsil } {
			set maxsil $sil
		}
	} else {
		set sil "-"
		set maxsil 0
	}

	set lastts $ts

	append output ", SIL=$sil/$maxsil"

	append output "\n        "

	foreach b $bytes {
		append output [format " %1x" $b]
	}

	return $output
}
